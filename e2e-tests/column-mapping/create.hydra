PREFIX api: <https://cube-creator.zazuko.com/vocab#>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX cc: <https://cube-creator.zazuko.com/vocab#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

ENTRYPOINT "project/ubd/csv-mapping/table-observation"

HEADERS {
  x-user "john-doe"
  x-permission "pipelines:write"
}

With Class api:Table {
    Expect Operation cc:CreateColumnMappingAction {
        Invoke {
            Content-Type "text/turtle"

            ```
            prefix cc: <https://cube-creator.zazuko.com/vocab#>
            prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
            prefix xsd: <http://www.w3.org/2001/XMLSchema#>

            <>  cc:sourceColumn <https://cube-creator.lndo.site/cube-project/ubd/csv-source/ubd/column/year> ;
                cc:targetProperty "dimension/year" ;
                cc:datatype xsd:gYear ;
                cc:language "en" ;
                cc:defaultValue "2000" .
            ```
        } => {
            Expect Status 201

            Expect Type <https://cube-creator.zazuko.com/vocab#ColumnMapping>
            Expect Property cc:targetProperty "dimension/year"
            Expect Property cc:sourceColumn {
                Expect Id <https://cube-creator.lndo.site/cube-project/ubd/csv-source/ubd/column/year>
            }
            Expect Property cc:datatype {
                Expect Id xsd:gYear
            }
            Expect Property cc:language "en"
            Expect Property cc:defaultValue "2000"
        }
    }
}
