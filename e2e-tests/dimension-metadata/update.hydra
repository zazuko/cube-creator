PREFIX cc:     <https://cube-creator.zazuko.com/vocab#>
PREFIX schema: <http://schema.org/>
PREFIX void:   <http://rdfs.org/ns/void#>
PREFIX cube:   <https://cube.link/>
PREFIX hydra:  <http://www.w3.org/ns/hydra/core#>
PREFIX qudt:   <http://qudt.org/schema/qudt/>

ENTRYPOINT "cube-project/ubd/dimensions-metadata/aggregation"

HEADERS {
  x-user "john-doe"
  x-permission "pipelines:write"
}

With Operation schema:UpdateAction {
    Invoke {
        Content-Type "text/turtle"

        ```
        base <https://cube-creator.lndo.site/>
        prefix qudt: <http://qudt.org/schema/qudt/>
        PREFIX schema: <http://schema.org/>

        <cube-project/ubd/dimensions-metadata/aggregation>
            schema:about <https://environment.ld.admin.ch/foen/ubd/28/aggregation> ;
            qudt:scaleType qudt:NominalScale ;
            schema:name "Aggregation"@en ;
            schema:description "The aggregation"@en.
        ```
    } => {
        Expect Status 200

        Expect Property schema:about {
            Expect Id <https://environment.ld.admin.ch/foen/ubd/28/aggregation>
        }
        Expect Property qudt:scaleType {
            Expect Id qudt:NominalScale
        }
        Expect Property schema:name "Aggregation"
        Expect Property schema:description "The aggregation"
    }
}
