PREFIX api: <https://cube-creator.zazuko.com/vocab#>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX cc: <https://cube-creator.zazuko.com/vocab#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>

HEADERS {
  x-user "john-doe"
  x-permission "pipelines:write"
}

With Class api:EntryPoint {
    Expect Property cc:projects {
        With Operation schema:CreateAction {
            Invoke {
                Content-Type "text/turtle"

                ```
                prefix cc: <https://cube-creator.zazuko.com/vocab#>
                prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                PREFIX schema: <http://schema.org/>
                PREFIX dcterms: <http://purl.org/dc/terms/>

                <> cc:projectSourceKind <https://cube-creator.lndo.site/shape/cube-project/create#CSV> ;
                   rdfs:label "Deleted Test Project" ;
                   dcterms:identifier "delete-test-cube" ;
                   schema:maintainer <https://cube-creator.lndo.site/organization/bafu> .
                ```
            } => {
                Expect Status 201

            }
        }
    }
}

With Class cc:CubeProject {
    Expect Link cc:csvMapping {
        Expect Property cc:csvSourceCollection {
            Expect Operation schema:CreateAction {
                Invoke {
                    Content-Type "text/turtle"

                    ```
                    prefix schema: <http://schema.org/>

                    <> schema:name "test.csv" ;
                       schema:contentUrl <http://s3.cube-creator.lndo.site/cube-creator/test-data/delete-test-cube/test.csv> ;
                       schema:identifier "test-data/delete-test-cube/test.csv" ;
                    ```
                } => {
                    Expect Status 201
                }
            }
        }
    }
    Expect Operation schema:DeleteAction {
        Invoke {
        } => {
            Expect Status 204
        }
    }
}
